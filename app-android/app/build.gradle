apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "de.tutao.tutanota"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            manifestPlaceholders = [contentProviderAuthority:'de.tutao.fileprovider.debug']
            applicationIdSuffix ".debug"
        }
        debugDist {
            initWith debug
            manifestPlaceholders = [contentProviderAuthority:'de.tutao.fileprovider.debugdist']
            applicationIdSuffix ".debugdist"
        }
        release {
            minifyEnabled false
            manifestPlaceholders = [contentProviderAuthority:'de.tutao.fileprovider']
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'hostname', '"' + InetAddress.getLocalHost().getHostName() + '"'
        it.buildConfigField 'String', 'FILE_PROVIDER_AUTHORITY', '"' + it.manifestPlaceholders['contentProviderAuthority'] + '"'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/ASL2.0'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "com.google.android.gms:play-services-gcm:10.2.1"
    implementation 'commons-io:commons-io:2.4'
    implementation 'org.jdeferred:jdeferred-core:1.2.4'
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:3.3.2"
    androidTestImplementation 'org.codehaus.jackson:jackson-mapper-asl:1.9.2'
    androidTestImplementation 'commons-codec:commons-codec:1.6'
}